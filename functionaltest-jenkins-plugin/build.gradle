plugins {
    id 'groovy'
    id 'java'
    id "codenarc"
    id "com.google.protobuf" version "0.8.17"
}

group 'functional-automation'
version '1.0-SNAPSHOT'

codenarc.configFile = file("./codenarc-rules.groovy")
codenarc.reportFormat = 'text'

sourceCompatibility = 1.8

test {
    testLogging.showStandardStreams = true
    testLogging.exceptionFormat = "full"
}

def grpcVersion = '1.21.0'
def protocVersion = '3.17.3'
def protobufVersion = '3.17.3'
def nettyTcNativeVersion = '2.0.25.Final'

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {
                outputSubDir = "java"
            }
        }

        // Add each output source directory to the sourceSet based on its basename (e.g., `java`).
        all().each { task ->
            task.outputSourceDirectorySet.srcDirs.each { srcDir ->
                sourceSets[task.sourceSet.name][srcDir.name].srcDirs += srcDir
            }
        }
    }
}

sourceSets {
    main {
        proto {
            // In addition to the default 'src/main/proto'
            srcDir '../stackrox-container-image-scanner/src/main/proto'
        }
    }
}


repositories {
    mavenCentral()
    maven {
        url 'https://repo.jenkins-ci.org/releases'
    }
    maven {
        url 'https://repo.jenkins-ci.org/public'
    }
}

// Assign all Java source dirs to Groovy, as the groovy compiler should take care of them.
sourceSets.each { ss ->
    ss.groovy.srcDirs += ss.java.srcDirs
    ss.java.srcDirs = []
}


dependencies {
    implementation 'com.google.code.gson:gson:2.7'
    implementation 'io.fabric8:kubernetes-client:4.1.2'
    implementation 'io.kubernetes:client-java:2.0.0-beta1'
    implementation 'org.codehaus.groovy:groovy-all:2.4.18'
    implementation group: 'com.jayway.restassured', name: 'rest-assured', version: '2.4.1'
    implementation group: 'com.github.docker-java', name: 'docker-java', version: '3.1.0-rc-3'
    implementation group: 'commons-cli', name: 'commons-cli', version: '1.2'
    implementation group: 'commons-httpclient', name: 'commons-httpclient', version: '3.1'
    implementation group: 'org.spockframework', name: 'spock-core', version: '1.2-groovy-2.4'
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.7'
    testImplementation group: 'junit', name: 'junit', version: '4.11'
    implementation group: 'com.offbytwo.jenkins', name: 'jenkins-client', version: '0.3.8'
    // JAX-B dependencies for JDK 9+
    implementation "jakarta.xml.bind:jakarta.xml.bind-api:2.3.2"
    implementation "org.glassfish.jaxb:jaxb-runtime:2.3.2"

    // grpc and protobuf
    implementation "com.google.api.grpc:proto-google-common-protos:2.3.2"
    implementation "io.grpc:grpc-alts:${grpcVersion}"
    implementation "io.grpc:grpc-netty:${grpcVersion}"
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation "io.grpc:grpc-auth:${grpcVersion}"
    implementation "com.google.protobuf:protobuf-java-util:${protobufVersion}"
    implementation "io.netty:netty-tcnative-boringssl-static:${nettyTcNativeVersion}"
}
